---
draft: true
date: 2023-01-11
---

# Using Terraform to Templatize REST API calls to Dialogflow

## Overview

[Dialogflow](https://cloud.google.com/dialogflow/cx/docs) is a powerful tool in
Google Cloud that you can use to design conversational agents powered by Natural
Language Understanding (NLU) to transform user requests into actionable data.
You can integrate voice and/or chat agents in your app, website, or customer
support systems to determine user intent and interact with users.

**Problem:** When
[managing Dialogflow Agents with Terraform](https://cloud.koverholt.com/managing-dialogflow-agents-with-terraform/),
sometimes the Dialogflow Terraform module that you are working with doesn't have
an argument for the specific setting you need, but the REST API does. If you
were to make standalone REST API calls, that configuration setting would not be
managed the same way as all of your other infrastructure as code! üò≠

**Solution:** Construct the REST API call that you want to use, then templatize
and manage your REST API call within your Terraform configuration files. Now you
can manage the settings that you need, and still get all of the benefits of
Terraform and infrastructure as code. üèÜ

In this post, we'll walk through the steps to construct and templatize a REST
API call in Terraform using the `local-exec` provisioner. Once imported into
Terraform, you can [manage all of your agents and their settings
programmatically](https://registry.terraform.io/providers/hashicorp/google/latest/docs/resources/dialogflow_cx_agent),
version control and track the state of your agents, spin up agents in
dev/QA/prod environments for testing, and all of the other neat functionality
that you get by using Terraform.

Lets's get to templatizing REST API calls in Terraform!

## Concepts

For the purposes of this blog post, we're going to focus on the concepts for
templatizing a REST API call for this agent with a very simplified Terraform
configuration (i.e., without complicating things by adding more Terraform
configuration files), whereas in practice you could represent this agent as well
as the REST API call completely in Terraform.

If you want to skip the concepts and just want to see Terraform code that solves
this problem, you can refer to the previous blog post on
[Managing Dialogflow Agents with Terraform](https://cloud.koverholt.com/managing-dialogflow-agents-with-terraform/)
and the accompanying [Terraform + Dialogflow code sample on
GitHub](https://github.com/koverholt/dialogflow-cx-terraform). Specifically, you
can refer to the
[`terraform/flows.tf`](https://github.com/koverholt/dialogflow-cx-terraform/blob/main/terraform/flows.tf)
file that uses a `local-exec` command to make a REST API call instead of using
Terraform to define the flow.

Otherwise, if you want to work through the problems of making a REST API call to
Dialogflow and the subsequent steps needed to templatize this REST API call in
Terraform, then read on!

## Setup

There are a few things that you'll need to set up before you can import a
Dialogflow agent with Terraform:

1. Register for a
   [Google Cloud account](https://cloud.google.com/docs/get-started).
1. Enable the
   [Dialogflow API](https://cloud.google.com/dialogflow/cx/docs/quick/setup).
1. Install and initialize the
   [Google Cloud `gcloud` CLI tool](https://cloud.google.com/sdk/docs/install).
1. Create a service account and associated JSON key per the
   [Terraform documentation](https://registry.terraform.io/providers/hashicorp/google/latest/docs/guides/provider_reference)
   and [Google Cloud](https://cloud.google.com/iam/docs/service-accounts)
   documentation.
1. Finally, install
   [Terraform](https://developer.hashicorp.com/terraform/downloads). If you're
   using macOS, I recommend installing Terraform with
   [Homebrew](https://brew.sh/). Or, even better, install
   [tfenv](https://github.com/tfutils/tfenv) using `brew install tfenv`, then
   run `tfenv install` so you can easily switch between versions of Terraform!

## Create a new pre-built agent

We'll start by creating a
[pre-built agent](https://cloud.google.com/dialogflow/cx/docs/concept/agents-prebuilt)
in Dialogflow CX. From the
[Dialogflow console](https://dialogflow.cloud.google.com/cx/projects) and your
associated project, click on `Use pre-built agents`, select the `Small talk`
agent, then click the `Create` button:

<center>
  <img src="/assets/images/templatizing-pre-built-agent.png" alt="Importing a pre-built agent in Dialogflow CX" width="800px">
</center>

Now, you'll have a simple Dialogflow agent with a few pages, intents, and
transition routes:

<center>
  <img src="/assets/images/templatizing-small-talk.png" alt="Small Talk Pre-Built Dialogflow CX agent" width="800px">
</center>

You can test the agent and have a sample dialog with it:

<center>
  <img src="/assets/images/templatizing-dialog.gif" alt="Sample dialog with Small Talk Dialogflow CX agent" width="500px">
</center>

## Edit training phrases in the default welcome intent

Now, assume that we wanted to modify the default welcome intent to include some
different training phrases instead of the default phrases:

<center>
  <img src="/assets/images/templatizing-default-welcome-intent.png" alt="Importing a pre-built agent in Dialogflow CX" width="800px">
</center>

### Why use Terraform?

We could modify the training phrases directly in the Dialogflow CX console, save
the intent, and move on. But what about infrastructure as code? What about the
reproducibility, change management, or version control of this modification?

Okay then, why not just
[import this agent into Terraform](https://cloud.koverholt.com/importing-existing-dialogflow-agents-into-terraform/)
and manage it as usual? You would have everything represented in
Terraform configuration files: the agent represented in `agents.tf` flows
represented in `flows.tf`, pages represented in `pages.tf`, and your Google
Cloud settings represented in `provider.tf` and `variables.tf`.

Problem solved, right? Not quite... read on.

### Why use REST API calls in Terraform?

There is an issue with this approach. If you were managing this Dialogflow agent
entirely with Terraform, you would run into an issue when attempting to create
or change the default welcome intent using Terraform. This happens because
Dialogflow creates a default welcome intent when the agent is created, and
Terraform isn't explicitly aware of the default welcome intent.

Since we can't modify the default welcome intent with Terraform, we could make a
[REST API](https://cloud.google.com/dialogflow/cx/docs/reference/rest) call to
modify the training phrases. Or, we could use the
[Dialogflow Python client library](https://cloud.google.com/dialogflow/cx/docs/reference/library/python)
to modify the training phrases.

Out of those options, the REST API or Python client would be represented as
code, which means that we could also manage them in Terraform. For the sake of
simplicity and not having to worry about managing a Python environment in
addition to Terraform, we'll construct a REST API call to modify the default
welcome intent, then we'll manage it with Terraform.

## Get information about an intent using a REST API call

Before we get into the details of Terraform and parameterizing REST API calls,
let's first construct a REST API call that gets information about the default
welcome intent.

Referring to
[REST API documentation for Dialogflow CX](https://cloud.google.com/dialogflow/cx/docs/reference/rest),
we see that there is a method to
[get information about an intent in Dialogflow](https://cloud.google.com/dialogflow/cx/docs/reference/rest/v3/projects.locations.agents.intents/get).
We can use this information to construct a REST API request that uses your
endpoint URL, Google Cloud project ID, agent ID, and the ID of the default
welcome intent:

```
export ENDPOINT=https://us-central1-dialogflow.googleapis.com
export PROJECT=your-project-id
export LOCATION=us-central1
export AGENT=5c6112d8-85bc-4432-b67e-c8d7090cc5c1
export INTENT=00000000-0000-0000-0000-000000000000

curl --location --request GET "${ENDPOINT}/v3/projects/${PROJECT}/locations/${LOCATION}/agents/${AGENT}/intents/${INTENT}" \
-H "Authorization: Bearer $(gcloud auth application-default print-access-token)"
```

When you execute this REST API request, you should see output with detailed
information about the default welcome intent, including the training phrases:

```
{
  "name": "projects/your-project-id/locations/us-central1/agents/5c6112d8-85bc-4432-b67e-c8d7090cc5c1/intents/00000000-0000-0000-0000-000000000000",
  "displayName": "default welcome intent",
  "trainingPhrases": [
    {
      "id": "19cd2285-4807-4ad5-b313-26f3191310e7",
      "parts": [
        {
          "text": "just going to say hi"
        }
      ],
      "repeatCount": 1
    },
    {
      "id": "59f99b0b-054e-415b-802b-f5901fcaf8c3",
      "parts": [
        {
          "text": "heya"
        }
      ],
      "repeatCount": 1
    },
[...]
```

Perfect! The intent object contains exactly the training phrases that we want to
modify. Now, let's move on to how to change the training phrases in the default
welcome intent using a REST API request.

## Modify an intent using a REST API call

Now, we'll construct a REST API call that makes the changes to the default
welcome intent that we want.

Referring to
[REST API documentation for Dialogflow CX](https://cloud.google.com/dialogflow/cx/docs/reference/rest),
we see that there are methods to
[`PATCH` or modify an intent in Dialogflow](https://cloud.google.com/dialogflow/cx/docs/reference/rest/v3/projects.locations.agents.intents/get)/.

We can use this information to construct a REST API request that uses your
endpoint URL, Google Cloud project ID, agent ID, and the ID of the default
welcome intent:

```
export ENDPOINT=https://us-central1-dialogflow.googleapis.com
export PROJECT=your-project-id
export LOCATION=us-central1
export AGENT=5c6112d8-85bc-4432-b67e-c8d7090cc5c1
export INTENT=00000000-0000-0000-0000-000000000000

curl --location --request PATCH "${ENDPOINT}/v3/projects/${PROJECT}/locations/${LOCATION}/agents/${AGENT}/intents/${INTENT}?updateMask=trainingPhrases" \
    -H "Authorization: Bearer $(gcloud auth application-default print-access-token)" \
    -H 'Content-Type: application/json' \
    --data-raw "
    {
      'trainingPhrases': [
        {'parts': [{'text': 'hi how are you'}], 'repeatCount': 1},
        {'parts': [{'text': 'greetings'}], 'repeatCount': 1},
        {'parts': [{'text': 'howdy'}], 'repeatCount': 1},
        {'parts': [{'text': 'hello'}], 'repeatCount': 1},
        {'parts': [{'text': 'hello there'}], 'repeatCount': 1},
        {'parts': [{'text': 'hi'}], 'repeatCount': 1},
        {'parts': [{'text': 'nice to meet you'}], 'repeatCount': 1},
        {'parts': [{'text': 'whats up'}], 'repeatCount': 1},
        {'parts': [{'text': 'hows it going'}], 'repeatCount': 1},
        {'parts': [{'text': 'hey'}], 'repeatCount': 1}
      ]
    }
    "
```

When you execute this REST API request, you should see a response with the newly
configured information in the default welcome intent, including the new training
phrases:

```
{}
  "name": "projects/your-project-id/locations/us-central1/agents/5c6112d8-85bc-4432-b67e-c8d7090cc5c1/intents/00000000-0000-0000-0000-000000000000",
  "displayName": "default welcome intent",
  "trainingPhrases": [
    {
      "parts": [
        {
          "text": "hi how are you"
        }
      ],
      "repeatCount": 1
    },
    {
      "parts": [
        {
          "text": "greetings"
        }
      ],
      "repeatCount": 1
    },
```

Success! The new default welcome intent contains the new training phrases that
we configured. Now, let's move on to how to templatize this REST API request and
manage it with Terraform.

## Invoke the REST API request from Terraform

Terraform has different provisioners for cloud platforms such as Google Cloud.
In addition to provisioners for cloud platforms, it also has other provisioners
such as the
[`local-exec` provisioner](https://developer.hashicorp.com/terraform/language/resources/provisioners/local-exec),
which can be used to run shell commands on the machine that we are running
Terraform from.

Terraform also has various resources, such as the modules that we are using for
Dialogflow CX. In addition to cloud resources, Terraform has a special resource
called the
[`null_resource`](https://registry.terraform.io/providers/hashicorp/null/latest/docs/resources/resource)
for arbitrary values/actions, which will be useful for us to use in conjunction
with the `local-exec` provisioner.

This is a nice way to manage any setting that is also available in the
Dialogflow CX REST API, or even add custom callbacks to other Google Cloud
services or tools when you need to.

To start, we'll create a new Terraform configuration file called `intents.tf`
with the following contents:

```terraform
resource "null_resource" "default_start_flow" {
  provisioner "local-exec" {
    command = <<-EOT
    export ENDPOINT=https://us-central1-dialogflow.googleapis.com
    export PROJECT=your-project-id
    export LOCATION=us-central1
    export AGENT=5c6112d8-85bc-4432-b67e-c8d7090cc5c1
    export INTENT=00000000-0000-0000-0000-000000000000

    curl --location --request PATCH "${ENDPOINT}/v3/projects/${PROJECT}/locations/${LOCATION}/agents/${AGENT}/intents/${INTENT}?updateMask=trainingPhrases" \
        -H "Authorization: Bearer $(gcloud auth application-default print-access-token)" \
        -H 'Content-Type: application/json' \
        --data-raw "
        {
          'trainingPhrases': [
            {'parts': [{'text': 'hi how are you'}], 'repeatCount': 1},
            {'parts': [{'text': 'greetings'}], 'repeatCount': 1},
            {'parts': [{'text': 'howdy'}], 'repeatCount': 1},
            {'parts': [{'text': 'hello'}], 'repeatCount': 1},
            {'parts': [{'text': 'hello there'}], 'repeatCount': 1},
            {'parts': [{'text': 'hi'}], 'repeatCount': 1},
            {'parts': [{'text': 'nice to meet you'}], 'repeatCount': 1},
            {'parts': [{'text': 'whats up'}], 'repeatCount': 1},
            {'parts': [{'text': 'hows it going'}], 'repeatCount': 1},
            {'parts': [{'text': 'hey'}], 'repeatCount': 1}
          ]
        }
        "
   EOT
  }
}
```

All we've done so far is to move our REST API command from the previous section,
the one that changes the default welcome intent, into a Terraform configuration
file. Specifically, we now have the REST API command wrapped with the
`local-exec` provisioner and contained in a `null_resource`.

This might work for what we need to accomplish. However, what if you want to use
a different Google Cloud project, or what if you want to use a different agent,
or is there a way we can populate the dynamic variables such as the agent ID
using other information that Terraform knows about? We'll work on that in the
next section.

## Using variables within a command in Terraform

To solve that problem, let's convert the static values into templated values in
Terraform. Instead of using `export VARIABLE=value` directly in the `local-exec`
command, let's remove those `export`s and instead use variables and
templating/interpolation in Terraform:

To solve that problem, let's convert the static values into templated values in
Terraform:

```terraform
resource "null_resource" "default_welcome_intent" {
  provisioner "local-exec" {
    command = <<-EOT
    curl --location --request PATCH "${self.triggers.ENDPOINT}/v3/projects/${self.triggers.PROJECT}/locations/${self.triggers.LOCATION}/agents/${self.triggers.AGENT}/intents/${self.triggers.INTENT}?updateMask=trainingPhrases" \
        -H "Authorization: Bearer $(gcloud auth application-default print-access-token)" \
        -H 'Content-Type: application/json' \
        --data-raw "
        {
          'trainingPhrases': [
            {'parts': [{'text': 'hi how are you'}], 'repeatCount': 1},
            {'parts': [{'text': 'greetings'}], 'repeatCount': 1},
            {'parts': [{'text': 'howdy'}], 'repeatCount': 1},
            {'parts': [{'text': 'hello'}], 'repeatCount': 1},
            {'parts': [{'text': 'hello there'}], 'repeatCount': 1},
            {'parts': [{'text': 'hi'}], 'repeatCount': 1},
            {'parts': [{'text': 'nice to meet you'}], 'repeatCount': 1},
            {'parts': [{'text': 'whats up'}], 'repeatCount': 1},
            {'parts': [{'text': 'hows it going'}], 'repeatCount': 1},
            {'parts': [{'text': 'hey'}], 'repeatCount': 1}
          ]
        }
        "
   EOT
  }

  triggers = {
    ENDPOINT = "https://us-central1-dialogflow.googleapis.com"
    PROJECT  = var.project_id
    LOCATION = var.region
    AGENT    = "5c6112d8-85bc-4432-b67e-c8d7090cc5c1"
    INTENT   = "00000000-0000-0000-0000-000000000000"
  }

}
```

Great! Now we have templatized the REST API request in Terraform. There's a few
different things going on here, so let's walk through the key points:

**Triggers:** We're using `triggers` here in Terraform instead of variables, the
reason is that we want to be able to re-use these values in a different
`local-exec` command in a later step to undo the customizations that we make

**Terraform variables:** The variables such as the `PROJECT` and `LOCATION` are
referring to Terraform variables that we've defined elsewhere, such as a file
called `variables.tf` with the following information:

```terraform
variable "project_id" {
  default = "your-project-id"
  type    = string
}

variable "region" {
  default = "us-central1"
  type    = string
}
```

**Endpoint URL:** For the endpoint URL, we could go a step further and
templatize the location within the endpoint URL, as in:

```
  triggers = {
    ENDPOINT = "https://${var.region}-dialogflow.googleapis.com"
  }
```

**Agent ID:** Instead of using a static value for the agent ID, ideally we would
be managing the agent within Terraform. Then, we could refer to the agent ID
using the following syntax:

```terraform
resource "google_dialogflow_cx_agent" "agent" {
  [...]
}

resource "null_resource" "default_welcome_intent" {
  [...]

  triggers = {
   AGENT = google_dialogflow_cx_agent.agent.name
  }
}
```

**Handling dependencies on components:**

Typically, we would be managing the agent within Terraform. In that case, we
want to ensure that the REST API call only runs after the agent exists by adding
a `depends_on` block inside of our `null_resource` that refers to our managed
agent's attribute in Terraform, as in:

```terraform
resource "null_resource" "default_welcome_intent" {
  [...]

  depends_on = [
    google_dialogflow_cx_agent.agent
  ]
}
```

## Undo changes with REST API calls in Terraform

Finally, when you modify Dialogflow settings that you created with a REST API
call, Terraform will likely fail when you try to change or manage the parent
component since you've changed these settings outside of Terraform (i.e., using
a REST API call).

In that case, we can use a different REST API call in a destroy-time provisioner
to reset or delete components in intents, flows, etc. To handle that, we'll add
a REST API call in another `local-exec` command block, which results in:

```
resource "null_resource" "default_welcome_intent" {
  provisioner "local-exec" {
    command = <<-EOT
    curl --location --request PATCH "${self.triggers.ENDPOINT}/v3/projects/${self.triggers.PROJECT}/locations/${self.triggers.LOCATION}/agents/${self.triggers.AGENT}/intents/${self.triggers.INTENT}?updateMask=trainingPhrases" \
        -H "Authorization: Bearer $(gcloud auth application-default print-access-token)" \
        -H 'Content-Type: application/json' \
        --data-raw "
        {
          'trainingPhrases': [
            {'parts': [{'text': 'hi how are you'}], 'repeatCount': 1},
            {'parts': [{'text': 'greetings'}], 'repeatCount': 1},
            {'parts': [{'text': 'howdy'}], 'repeatCount': 1},
            {'parts': [{'text': 'hello'}], 'repeatCount': 1},
            {'parts': [{'text': 'hello there'}], 'repeatCount': 1},
            {'parts': [{'text': 'hi'}], 'repeatCount': 1},
            {'parts': [{'text': 'nice to meet you'}], 'repeatCount': 1},
            {'parts': [{'text': 'whats up'}], 'repeatCount': 1},
            {'parts': [{'text': 'hows it going'}], 'repeatCount': 1},
            {'parts': [{'text': 'hey'}], 'repeatCount': 1}
          ]
        }
        "
   EOT
  }

  # Use triggers instead of environment variables so that they can be reused in
  # the provisioner to create routes as well as the destroy-time provisioner
  triggers = {
    ENDPOINT = "https://us-central1-dialogflow.googleapis.com"
    PROJECT  = var.project_id
    LOCATION = var.region
    AGENT    = "5c6112d8-85bc-4432-b67e-c8d7090cc5c1"
    INTENT   = "00000000-0000-0000-0000-000000000000"
  }

  # Use a REST API call in a destroy-time provisioner to reset training phrases
  # in the default welcome intent since we created them with an externally
  # managed REST API call
  provisioner "local-exec" {
    when    = destroy
    command = <<-EOT
    curl --location --request PATCH "${self.triggers.ENDPOINT}/v3/projects/${self.triggers.PROJECT}/locations/${self.triggers.LOCATION}/agents/${self.triggers.AGENT}/intents/${self.triggers.INTENT}?updateMask=trainingPhrases" \
        -H "Authorization: Bearer $(gcloud auth application-default print-access-token)" \
        -H 'Content-Type: application/json' \
        --data-raw "
        {
          'trainingPhrases': [
            {'parts': [{'text': 'hello'}], 'repeatCount': 1}
          ]
        }
        "
   EOT
  }
}
```

That way, when we run `terraform destroy`, Terraform will run the destroy-time
provisioner and the command that resets the training phrases. The only
difference here is that we added another REST API call with one training phrase,
and we added the `when = destroy` direction to that `local-exec` provisioner to
indicate that the second REST API call should only be used at destroy time.

## Summary

In this post, we walked through the steps to construct and templatize a REST API
call to Dialogflow CX in Terraform using the `local-exec` provisioner. You can
use this approach to manage all of the custom agents settings that you need, and
still get all of the benefits of Terraform and infrastructure as code.

I tried to keep things simple(r) by keeping the `null_resource` and `local-exec`
concepts in Terraform self-contained within this blog post along with using a
pre-built Dialogflow agent as an example.

For a more complete example, you can refer to the previous blog post on
[Managing Dialogflow Agents with Terraform](https://cloud.koverholt.com/managing-dialogflow-agents-with-terraform/)
and the accompanying [Terraform + Dialogflow code sample on
GitHub](https://github.com/koverholt/dialogflow-cx-terraform).

In that example, you can see that the
[`terraform/flows.tf`](https://github.com/koverholt/dialogflow-cx-terraform/blob/main/terraform/flows.tf)
file actually uses a `local-exec` command to make a REST API call instead of
using Terraform to define the flow. This approach was required in that example
since Dialogflow creates a default start flow when the agent is created, and
Terraform isn't explicitly aware of the default start flow and will fail to
delete the agent since it has external changes/dependencies that were performed
by Dialogflow rather than Terraform.

Hopefully you find these examples useful for templatizing REST API calls in
Terraform when you're managing Dialogflow agents, or even using REST API calls
to other Google Cloud services.

Happy templatizing!
